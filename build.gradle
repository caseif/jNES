plugins {
    id 'java'
    id 'maven'
    id 'idea'
    id 'eclipse'

    id 'net.minecrell.licenser' version '0.3'
    id 'com.github.johnrengelman.shadow' version '2.0.2'
}

defaultTasks 'clean', 'updateLicenses', 'build', 'shadowJar'

group = 'net.caseif.jnes'
version = '0.1.0-SNAPSHOT'

// Extended project information
ext.projectName = 'jnes'
ext.inceptionYear = '2018'
ext.packaging = 'jar'

sourceCompatibility = '1.8'
targetCompatibility = '1.8'

// Read source files using UTF-8
tasks.withType(JavaCompile) {
    options.encoding = 'UTF-8'
}

task wrapper(type: Wrapper) {
    gradleVersion = '4.6'
}

license {
    header getRootProject().file('HEADER.txt')
    exclude '**/lib/**/*.java'
}

repositories {
    mavenCentral()
}

dependencies {
    shadow 'com.google.guava:guava:24.1-jre'
}

jar.manifest.mainAttributes(
        'Implementation-Title': name,
        'Implementation-Version': version
)

jar {
    classifier = 'base'
    manifest {
        attributes(
                'Implementation-Title': project.name,
                'Implementation-Version': version,
                'Main-Class': 'net.caseif.jnes.Main'
        )
    }
}

task sourceJar(type: Jar) {
    from sourceSets.main.java
    from sourceSets.main.resources
    classifier = 'sources'
}

shadowJar {
    classifier = null
    configurations = [project.configurations.shadow]
}

artifacts {
    archives shadowJar
    archives sourceJar
}
